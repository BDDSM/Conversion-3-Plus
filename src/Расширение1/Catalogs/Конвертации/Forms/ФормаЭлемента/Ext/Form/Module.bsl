#Область ОписаниеПеременных

&НаКлиенте
Перем КонсольКодаПередКонвертацией;

&НаКлиенте
Перем КонсольКодаПередОтложеннымЗаполнением;

&НаКлиенте
Перем КонсольКодаПослеКонвертации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура Расш1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Элементы.АлгоритмПередКонвертацией.Видимость = Ложь;
	Элементы.АлгоритмПередОтложеннымЗаполнением.Видимость = Ложь;
	Элементы.АлгоритмПослеКонвертации.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	
	ПередКонвертациейHTML = АдресКонсолиКода;
	ПередОтложеннымЗаполнениемHTML = АдресКонсолиКода;
	ПослеКонвертацииHTML = АдресКонсолиКода;
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	
	// Запишем новый текст в реквизиты.
	
	Объект.АлгоритмПередКонвертацией = КонсольКодаПередКонвертацией.getText();
	
	Объект.АлгоритмПередОтложеннымЗаполнением = КонсольКодаПередОтложеннымЗаполнением.getText();
	
	Объект.АлгоритмПослеКонвертации = КонсольКодаПослеКонвертации.getText();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Расш1_ПередКонвертациейHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПередКонвертацией = Элементы.ПередКонвертациейHTML.Документ.defaultView;
	ИнициализацияРедактора(КонсольКодаПередКонвертацией, Объект.АлгоритмПередКонвертацией);
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПередОтложеннымЗаполнениемHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПередОтложеннымЗаполнением = Элементы.ПередОтложеннымЗаполнениемHTML.Документ.defaultView;
	ИнициализацияРедактора(КонсольКодаПередОтложеннымЗаполнением, Объект.АлгоритмПередОтложеннымЗаполнением);
	
КонецПроцедуры

&НаКлиенте
Процедура Расш1_ПослеКонвертацииHTMLДокументСформированПосле(Элемент)
	
	КонсольКодаПослеКонвертации = Элементы.ПослеКонвертацииHTML.Документ.defaultView;
	ИнициализацияРедактора(КонсольКодаПослеКонвертации, Объект.АлгоритмПослеКонвертации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализацияРедактора(ВнешнийОбъект, РеквизитОбъекта)
	
	Инфо = Новый СистемнаяИнформация();
	ВнешнийОбъект.init(Инфо.ВерсияПриложения);
	
	// Очистим элемент от предыдущих значений.
	ВнешнийОбъект.eraseText();
	// Прочитаем текст из реквизита.
	ВнешнийОбъект.setText(РеквизитОбъекта);
	
КонецПроцедуры

#КонецОбласти
